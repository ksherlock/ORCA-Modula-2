DEFINITION MODULE EZStorage;
(*
  Standard dynamic storage management

  Storage management for dynamic variables. Calls to the Modula-2 standard
  procedures NEW and DISPOSE are translated into calls to ALLOCATE and
  DEALLOCATE. The standard way to provide these two procedures is to import
  them from this module 'Storage'.

  Memory is allocated with these attributes:

      attrLocked    - It must not move as it is being referenced by a pointer
      attrFixed     - Ditto
      attrNoSpec    - Do not use special memory
*)

FROM SYSTEM IMPORT
  ADDRESS;

PROCEDURE ALLOCATE(VAR a: ADDRESS; size: CARDINAL);
(*
  OPERATION:
    Allocate some dynamic storage (contiguous memory area).

  NOTE:
    The actual number of bytes allocated may be slightly greater than 'size',
    due to administrative overhead.

  Errors:
    If not enough space is available, or when attempting to allocate more than
    65520 (0FFF0H) bytes at once, then 'a' is returned as NIL.
*)

PROCEDURE ALLOCATESTACK(VAR a: ADDRESS; size: CARDINAL);
(*
  OPERATION:
    Allocate some dynamic storage (contiguous memory area).  The difference
    between this and ALLOCATE, is that this procedure will ask for the memory
    to be allocated from bank zero, meaning it can be used as either stack
    space or directpage space.

    This sort of memory is needed when starting up tools manualy, or before
    starting a new process.

  NOTE:
    The actual number of bytes allocated may be slightly greater than 'size',
    due to administrative overhead.

  Errors:
    If not enough space is available, or when attempting to allocate more than
    65520 (0FFF0H) bytes at once, then 'a' is returned as NIL.
*)

PROCEDURE ChangeSize(a: ADDRESS; newSize: CARDINAL; VAR done: BOOLEAN);
(*
  OPERATION:
    Changes the size of the block to the specified size.  Note that due to the
    Fixed and Locked attributes use at allocation time, that the block will
    not be moved, so the change in size will not always work.  This is best
    used when trying to reduce the size of the block.
*)

PROCEDURE DEALLOCATE (VAR a: ADDRESS; size: CARDINAL);
(*
  OPERATION:
    Release some dynamic storage (contiguous memory area).

  NOTE:
    The storage area released is made available for subsequent calls to
    ALLOCATE.

    'a' is set to NIL as a result of the deallocation.
*)

PROCEDURE Available (size: CARDINAL) : BOOLEAN;
(*
  OPERATION:
    Returns TRUE if there exists a block of memory at lease 'size' bytes in
    size.
*)

END EZStorage.
